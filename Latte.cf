-- programs ------------------------------------------------

entrypoints Program ;

Prog.       Program ::= [TopDef] ;

FnDef.      TopDef ::= Type Ident "(" [Arg] ")" Block ;

ClDef.      TopDef ::= "class" Ident ClBlock ;

ClDefExt.   TopDef ::= "class" Ident "extends" Ident ClBlock ;

separator nonempty TopDef "" ;

Ar. 	   Arg ::= Type Ident;

separator  Arg "," ;

-- classes -------------------------------------------------

ClBlk.      ClBlock ::= "{" [ClMember] "}";

ClFields.    ClMember ::= Type [ClFieldItem] ";" ;

ClFieldItemNoInit. ClFieldItem ::= Ident ;

separator nonempty ClFieldItem "," ;

ClMethod.   ClMember ::= Type Ident "(" [Arg] ")" Block ;

separator   ClMember "" ;


-- statements ----------------------------------------------

Blk.       Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr6 "=" Expr  ";" ;

Incr.      Stmt ::= Expr "++"  ";" ;

Decr.      Stmt ::= Expr "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Arg ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Class.     Type ::= Ident ;

Array.     Type ::= Type "[]" ;

separator  Type "," ;

-- Expressions ---------------------------------------------

ENewArray.  Expr6 ::= "new" Type "[" Expr "]" ;

EArrayElem. Expr7 ::= Expr6 "[" Expr "]";

EArrayElemR. Expr7 ::= Ident "[" Expr "]";

ENullArr.   Expr6 ::= "(" Type "[]" ")" "null" ;

ENull.      Expr6 ::= "(" Ident ")" "null" ;

ENil.       Expr6 ::= "null" ;

ENew.       Expr6 ::= "new" Type ;

EMethod.    Expr6 ::= Expr6 "." Ident "(" [Expr] ")" ;

EField.     Expr6 ::= Expr6 "." Ident ;

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr5 ;

Not.       Expr5 ::= "!" Expr5 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

